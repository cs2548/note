<!DOCTYPE HTML>
<html>
<head>
<title>工具提示样式的鼠标菜单 - 脚本示例 | AutoHotkey</title>
<meta name="description" content="The tooltip mouse menu script displays a popup menu in response to briefly holding down the middle mouse button." />
<meta http-equiv="content-type" content="text/html; charset=utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>工具提示样式的鼠标菜单(需要 XP/2k/NT) <span class="headnote">作者: Rajat</span></h1>

<p>此脚本显示弹出菜单来响应短暂地按下鼠标中键的动作. 左键点击来选择菜单项. 在菜单外点击来取消菜单. 最近的改进是可以根据活动的窗口类型来改变菜单的内容(这里使用了记事本和 Word 作为例子).
</p>
<p><a href="TooltipMouseMenu.ahk">下载此脚本</a> &nbsp;| &nbsp;<a href="index.htm">其他示例脚本</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">主页</a></p>

<pre class="NoIndent"><em>; You can set any title here for the menu:</em>
MenuTitle = -=-=-=-=-=-=-=-

<em>; This is how long the mouse button must be held to cause the menu to appear:</em>
UMDelay = 20

SetFormat, float, 0.0
SetBatchLines, 10ms 
SetTitleMatchMode, 2
#SingleInstance


<em>;___________________________________________
;_____Menu Definitions______________________</em>

<em>; Create / Edit Menu Items here.
; You can't use spaces in keys/values/section names.</em>

<em>; Don't worry about the order, the menu will be sorted.</em>

MenuItems = Notepad/Calculator/Section 3/Section 4/Section 5


<em>;___________________________________________
;______Dynamic menuitems here_______________</em>

<em>; Syntax:
;     Dyn# = MenuItem|Window title</em>

Dyn1 = MS Word|- Microsoft Word
Dyn2 = Notepad II|- Notepad

<em>;___________________________________________</em>

Exit


<em>;___________________________________________
;_____Menu Sections_________________________</em>

<em>; Create / Edit Menu Sections here.</em>

Notepad:
Run, Notepad.exe
Return

Calculator:
Run, Calc
Return

Section3:
MsgBox, You selected 3
Return

Section4:
MsgBox, You selected 4
Return

Section5:
MsgBox, You selected 5
Return

MSWord:
msgBox, this is a dynamic entry (word)
Return

NotepadII:
msgBox, this is a dynamic entry (notepad)
Return


<em>;___________________________________________
;_____Hotkey Section________________________</em>

~MButton::
HowLong = 0
Loop
{
    HowLong ++
    Sleep, 10
    GetKeyState, MButton, MButton, P
    IfEqual, MButton, U, Break
}
IfLess, HowLong, %UMDelay%, Return


<em>;prepares dynamic menu</em>
DynMenu =
Loop
{
    IfEqual, Dyn%A_Index%,, Break

    StringGetPos, ppos, dyn%A_Index%, |
    StringLeft, item, dyn%A_Index%, %ppos%
    ppos += 2
    StringMid, win, dyn%A_Index%, %ppos%, 1000

    IfWinActive, %win%,
        DynMenu = %DynMenu%/%item%
}

<em>;Joins sorted main menu and dynamic menu</em>
Sort, MenuItems, D/
TempMenu = %MenuItems%%DynMenu%


<em>;clears earlier entries</em>
Loop
{
    IfEqual, MenuItem%A_Index%,, Break
    MenuItem%A_Index% =
}

<em>;creates new entries</em>
Loop, Parse, TempMenu, /
{
    MenuItem%A_Index% = %A_LoopField%
}

<em>;creates the menu</em>
Menu = %MenuTitle%
Loop
{
    IfEqual, MenuItem%A_Index%,, Break
    numItems ++
    StringTrimLeft, MenuText, MenuItem%A_Index%, 0
    Menu = %Menu%`n%MenuText%
}

MouseGetPos, mX, mY
Hotkey, ~LButton, MenuClick
Hotkey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
Hotkey, ~LButton, Off
IfWinNotActive, %MenuTitle%
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <em>;space after which first line starts</em>
mY /= 13    <em>;space taken by each line</em>
IfLess, mY, 1, Return
IfGreater, mY, %numItems%, Return
StringTrimLeft, TargetSection, MenuItem%mY%, 0
StringReplace, TargetSection, TargetSection, %A_Space%,, A
Gosub, %TargetSection%
Return
</pre>
</body>
</html>